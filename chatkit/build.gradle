apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 1
        versionName "0.2.2"

        consumerProguardFiles 'proguard.txt'
    }
    android {
        lintOptions {
            abortOnError false
        }
    }
}

def chatKitVersion = "git describe --abbrev=0 --tags".execute().text.trim()
def chatKitFullVersion = "git describe --always --tags".execute().text.trim()

group = 'io.skygear'
version = chatKitFullVersion

def bintrayUser = System.getenv('BINTRAY_USER')
def bintrayApiKey = System.getenv('BINTRAY_API_KEY')

if (project.rootProject.file('local.properties').exists()) {
    Properties localProperties = new Properties()
    localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

    bintrayUser = localProperties.getProperty('bintrayUser', bintrayUser)
    bintrayApiKey = localProperties.getProperty('bintrayApiKey', bintrayApiKey)
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'ChatKit'
        userOrg = 'skygeario'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SkygearIO/ChatKit.git'
        publish = true
        version {
            name = chatKitFullVersion
            desc = "ChatKit - is a library designed to simplify the development of UI for such a trivial task as chat. It have flexible possibilities for styling, customizing and data management"
            released  = new Date()
            vcsTag = chatKitVersion
        }
    }
}

task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

artifacts {
    archives generateSourcesJar
}

ext {
    supportVersion = '25.3.1'
    flexboxVersion = '0.2.5'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$supportVersion"
    compile "com.android.support:design:$supportVersion"
    compile "com.google.android:flexbox:$flexboxVersion"
}
